plugins {
    id("com.diffplug.spotless") version "6.13.0"
    id("org.jreleaser") version "1.19.0"
}

ext {
    projectGroup = 'com.scalar-labs'
    projectVersion = '3.12.0-SNAPSHOT'
}

group = projectGroup
version = projectVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.diffplug.spotless'

    group = projectGroup
    project.version = projectVersion

    ext {
        grpcVersion = '1.73.0'
        protocVersion = '3.25.8'
        picoCliVersion = '4.7.7'
        guiceVersion = '5.1.0'
        guavaVersion = '32.1.3-jre'
        slf4jVersion = '1.7.36'
        log4jVersion = '2.25.0'
        jsonpVersion = '1.1.4'
        jacksonVersion = '2.19.1'
        toml4jVersion = '0.7.2'
        scalarDbVersion = '3.16.0'
        scalarAdminVersion = '2.2.1'
        resilience4jRetryVersion = '1.7.1'
        dropwizardMetricsVersion = '4.2.33'
        prometheusVersion = '0.16.0'
        jettyVersion = '9.4.57.v20241219'
        assertjVersion = '3.27.3'
        junitVersion = '5.13.3'
        junitPlatformVersion = '1.13.3'
        mockitoVersion = '4.11.0'
        errorproneVersion = '2.10.0'
        errorproneJavacVersion = '9+181-r4173-1'
        errorpronePluginVersion = '2.0.2'
        spotbugsVersion = '4.8.6'
        spotbugsPluginVersion = '5.2.5'
        commonsTextVersion = '1.13.1'
        stefanbirknerSystemLambdaVersion = '1.2.1'
        googleJavaFormatVersion = '1.7'
        dockerPluginVersion = '0.34.0'
        bouncyCastleCryptoVersion = '1.70'
        partiqlVersion = '1.2.2'

        dockerVersion = project.properties['dockerVersion'] ?: project.version
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
        implementation(group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}") {
            exclude group: 'com.github.spotbugs', module: 'spotbugs-annotations'
        }
        implementation(group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4jVersion}") {
            exclude group: 'com.github.spotbugs', module: 'spotbugs-annotations'
        }
        implementation group: 'com.google.inject', name: 'guice', version: "${guiceVersion}"
        implementation group: 'com.google.guava', name: 'guava', version: "${guavaVersion}"
        implementation group: 'javax.json', name: 'javax.json-api', version: "${jsonpVersion}"
        implementation group: 'org.glassfish', name: 'javax.json', version: "${jsonpVersion}"
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
        // Import this module to use ConsoleLauncher to execute :ledger:permissionTest LedgerServicePermissionTest.
        testImplementation group: 'org.junit.platform', name: 'junit-platform-console', version: "${junitPlatformVersion}"
        testImplementation group: 'org.assertj', name: 'assertj-core', version: "${assertjVersion}"
        testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
        testImplementation group: 'org.mockito', name: 'mockito-inline', version: "${mockitoVersion}"
    }

    test {
        maxHeapSize "512m"
        testLogging.showStandardStreams = true

        useJUnitPlatform()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    distZip {
        archiveFileName = "${project.name}.zip"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        dependsOn += test
    }

    distTar {
        archiveFileName = "${project.name}.tar"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        dependsOn += test
    }

    installDist {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task dockerfileLint(type: Exec) {
        description 'Lint the Dockerfile'
        executable "${rootDir}/scripts/docker/lint.sh"
    }
}

jreleaser {
    gitRootSearch = true

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            def stagingRepositories = ['client/build/staging-deploy',
                                       'common/build/staging-deploy',
                                       'ledger/build/staging-deploy',
                                       'rpc/build/staging-deploy']
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepositories.each { stagingRepository(it) }
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepositories.each { stagingRepository(it) }
                }
            }
        }
    }
}
