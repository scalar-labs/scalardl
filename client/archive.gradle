apply plugin: 'maven-publish'

// Check if the version is a feature snapshot (e.g., 4.0.0-feature-xxx-SNAPSHOT)
def isFeatureSnapshot = project.version.toString().contains('-feature-')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'scalardl-java-client-sdk'
            from components.java
            artifact distTar
            artifact distZip
            artifact javadocJar
            artifact sourcesJar
            pom {
                name = 'ScalarDL Java Client SDK'
                description = 'A client-side Java library to interact with ScalarDL.'
                url = 'https://github.com/scalar-labs/scalardl-java-client-sdk'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'hiroyuki'
                        name = 'Hiroyuki Yamada'
                        email = 'hiroyuki.yamada@scalar-labs.com'
                    }
                    developer {
                        id = 'jnmt'
                        name = 'Jun Nemoto'
                        email = 'jun.nemoto@scalar-labs.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/scalar-labs/scalardl-java-client-sdk.git'
                    developerConnection = 'scm:git:https://github.com/scalar-labs/scalardl-java-client-sdk.git'
                    url = 'https://github.com/scalar-labs/scalardl-java-client-sdk'
                }
            }
        }
    }
    repositories {
        if (isFeatureSnapshot) {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/scalar-labs/scalardl')
                credentials {
                    username = project.findProperty('gprUsername') ?: System.getenv('GPR_USERNAME')
                    password = project.findProperty('gprPassword') ?: System.getenv('GPR_PASSWORD')
                }
            }
        } else {
            maven {
                name = 'staging'
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}
