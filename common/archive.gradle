apply plugin: 'maven-publish'

// Check if the version is a feature snapshot (e.g., 4.0.0-feature-xxx-SNAPSHOT)
def isFeatureSnapshot = project.version.toString().contains('-feature-')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'scalardl-common'
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom {
                name = 'ScalarDL Common'
                description = 'ScalarDL Common libraries.'
                url = 'https://github.com/scalar-labs/scalardl'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'hiroyuki'
                        name = 'Hiroyuki Yamada'
                        email = 'hiroyuki.yamada@scalar-labs.com'
                    }
                    developer {
                        id = 'jnmt'
                        name = 'Jun Nemoto'
                        email = 'jun.nemoto@scalar-labs.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/scalar-labs/scalardl.git'
                    developerConnection = 'scm:git:https://github.com/scalar-labs/scalardl.git'
                    url = 'https://github.com/scalar-labs/scalardl'
                }
            }
        }
    }
    repositories {
        if (isFeatureSnapshot) {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/scalar-labs/scalardl')
                credentials {
                    username = project.findProperty('gprUsername') ?: System.getenv('GPR_USERNAME')
                    password = project.findProperty('gprPassword') ?: System.getenv('GPR_PASSWORD')
                }
            }
        } else {
            maven {
                name = 'staging'
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}
