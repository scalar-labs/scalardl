plugins {
    id 'application'
    id 'net.ltgt.errorprone' version "${errorpronePluginVersion}"
    id "com.github.spotbugs" version "${spotbugsPluginVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

startScripts.enabled = false

sourceSets {
    integrationTest {
        java {
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }

    // Everything below has been added as there is a bug in Gradle that prevents us from
    // creating the security manager. If this ever gets fixed we can move the permission
    // test to the unit tests directory and delete everything below.
    permissionTest {
        java {
            srcDir file('src/permission-test/java')
        }
        resources.srcDir file('src/permission-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestCompileOnly.extendsFrom testCompileOnly
    permissionTestImplementation.extendsFrom testImplementation
    permissionTestRuntimeOnly.extendsFrom testRuntimeOnly
    permissionTestCompileOnly.extendsFrom testCompileOnly
}

dependencies {
    implementation project(':common')
    implementation group: 'com.moandjiezana.toml', name: 'toml4j', version: "${toml4jVersion}"
    implementation group: 'info.picocli', name: 'picocli', version: "${picoCliVersion}"
    implementation group: 'io.github.resilience4j', name: 'resilience4j-retry', version: "${resilience4jRetryVersion}"

    // The following packages are provided for ScalarDL contracts in runtime
    implementation group: 'org.hashids', name: 'hashids', version: '1.0.3'
    implementation group: 'com.github.erosb', name: 'everit-json-schema', version: '1.14.6'

    testImplementation group: 'com.scalar-labs', name: 'scalardb-schema-loader', version: "${scalarDbVersion}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${junitVersion}"
    integrationTestImplementation sourceSets.main.output
    integrationTestImplementation sourceSets.test.output
    integrationTestImplementation project(':common-test')
    permissionTestImplementation sourceSets.main.output
    permissionTestImplementation sourceSets.test.output

    // for Error Prone
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    errorproneJavac "com.google.errorprone:javac:${errorproneJavacVersion}"

    // for SpotBugs
    spotbugs "com.github.spotbugs:spotbugs:${spotbugsVersion}"
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

javadoc {
    title = "ScalarDL Ledger ${version}"
}

task testJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

task LedgerServer(type: CreateStartScripts) {
    mainClass = 'com.scalar.dl.ledger.server.LedgerServer'
    applicationName = 'scalar-ledger'
    defaultJvmOpts = ["-Djava.security.manager", "-Djava.security.policy=security.policy"]
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

distributions {
    main {
        contents {
            into('bin') {
                from(LedgerServer)
                filePermissions {
                    unix('0755')
                }
            }
        }
    }
}

task copyFilesToDockerBuildContextDir(type: Copy) {
    description = 'Copy files to a temporary folder to build the Docker image'
    dependsOn distTar
    from('Dockerfile')
    from('conf') {
        include 'ledger.properties.for.docker'
        include 'log4j2.properties'
    }
    from('docker-entrypoint.sh')
    from(tasks.distTar.archiveFile)
    into('build/docker')
}

task docker(type: Exec) {
    description = 'Build ScalarDL Ledger Docker image'
    dependsOn copyFilesToDockerBuildContextDir
    workingDir 'build/docker'
    commandLine 'docker', 'build', '--tag', "ghcr.io/scalar-labs/scalardl-ledger:${dockerVersion}", '.'
}

def integrationTestBase = {
    group 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false } // ensures integration tests are run every time when called
    jvmArgs '-Djava.security.manager=allow'
    options {
        systemProperties(System.getProperties().findAll{it.key.toString().startsWith("scalar")})
    }
}

task integrationTest(type: Test) {
    description 'Runs the integration tests.'
    configure integrationTestBase
}

task integrationTestCosmos(type: Test) {
    description 'Runs the integration tests for Cosmos DB.'
    configure integrationTestBase
    jvmArgs '-Xmx6g', '-XX:MaxDirectMemorySize=4g'
}

task permissionTest(type: Test) {
    description 'Runs the permission tests.'
    group 'verification'
    testClassesDirs = sourceSets.permissionTest.output.classesDirs
    classpath = sourceSets.permissionTest.runtimeClasspath
    outputs.upToDateWhen { false } // ensures permission tests are run every time when called
    jvmArgs '-Djava.security.manager=allow'
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        googleJavaFormat(googleJavaFormatVersion)
    }
}

spotbugs {
    ignoreFailures = false
    showStackTraces = true
    showProgress = true
    effort = 'default'
    reportLevel = 'default'
    maxHeapSize = '1g'
    extraArgs = [ '-nested:false' ]
    jvmArgs = [ '-Duser.language=en' ]
}

spotbugsMain.reports {
    html.enabled = true
}

spotbugsTest.reports {
    html.enabled = true
}

spotbugsIntegrationTest.reports {
    html.enabled = true
}

check.dependsOn -= integrationTest  // build should not depend on the integration tests

task copyTestJarsToTestLib(type: Copy) {
    from configurations.testRuntimeClasspath
    into file("$buildDir/test-libs")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test.dependsOn += copyTestJarsToTestLib
test.dependsOn += compileIntegrationTestJava
test.dependsOn += compilePermissionTestJava

distZip {
    mustRunAfter tasks.named('compileIntegrationTestJava')
    mustRunAfter tasks.named('compilePermissionTestJava')
}

distTar {
    mustRunAfter tasks.named('compileIntegrationTestJava')
    mustRunAfter tasks.named('compilePermissionTestJava')
}

base {
    archivesName = "scalardl-ledger"
}

// for archiving and uploading to maven central
if (!project.gradle.startParameter.taskNames.isEmpty() &&
   (project.gradle.startParameter.taskNames[0].endsWith('publish') ||
    project.gradle.startParameter.taskNames[0].endsWith('publishToMavenLocal'))) {
    apply from: 'archive.gradle'
}
